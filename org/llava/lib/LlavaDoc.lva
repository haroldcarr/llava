;;;; Copyright (c) 1997 - 2004 Harold Carr
;;;;
;;;; This work is licensed under the Creative Commons Attribution License.
;;;; To view a copy of this license, visit 
;;;;   http://creativecommons.org/licenses/by/2.0/
;;;; or send a letter to
;;;;   Creative Commons, 559 Nathan Abbott Way, Stanford, California 94305, USA
;;;;---------------------------------------------------------------------------

;;;;
;;;; Created       : 2004 Sep 03 (Fri) 18:32:41 by Harold Carr.
;;;; Last Modified : 2005 Feb 16 (Wed) 14:09:10 by Harold Carr.
;;;;

(package org.llava.lib.LlavaDoc)

(import org.llava.lib.Files)
(import org.llava.lib.io.FileWalker)
(import org.llava.lib.scm.Lists)

(-package-
 (description
  "Generates package and procedure documentation in DocBook format."))

(-comment-
-doc- for llavadoc only; handwrite the intro/book
-package- -> section with title same a (package ...) 
             or (-package- (location ...))
-doc-     -> refentry

refentry id must be full path to be unique, 
But need to strip path and only have name for alpha function list

-package-
-section-
-section-end-
    These are only meant to be used in the builtin file.
    Therefore they do not have empty macro definitions.

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Single builtin file:

(-package-
 (location org.llava)
 (description "..."))

(-section-
 (name "Creating a Java Object")
 (description "..."))
(-doc-
 ...)
(-section-end-)

(-section- 
 (name "Virtual Method Invocation")
 (description "..."))
(-doc-
 ...)
(-section-end-)

(-section- (name "Static Field Access"))
(-doc-
 ...)
(-section-end-)

(-section- (name "Procedures"))
(-section- (name "Numbers"))
(-doc-
 ...)
(-section-end-)
(-section-end-)

generates:

<section id="org.llava">
  <title>org.llava</title>
  ...description...
  <section id="CreatingAJavaObject">
    <title>Creating a Java Object</title>
    <refentry>...</refentry>
  </section>
  <section id="VirtualMethodInvocation">
    <title>Virtual Method Invocation</title>
    <refentry>...</refentry>
  </section>
  <section id="StaticFieldAccess">
    <title>Static Field Access</title>
    <refentry>...</refentry>
  </section>
  <section id="Procedures">
    <title>Procedures</title>
    <section id="Numbers">
      <title>Numbers</title>
    </section>
  </section>
</section>

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Library file:

(package org.foo.Lists)

(-package-
 (description "..."))

(-doc-
 ...)

generates:

<section id="org.foo.Lists">
  <title>org.foo.Lists</title>
  ...description...
  <refentry>...</refentry>
</section>

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Site:

chapter Download
chapter llava features
 &org.llava;
)

;;;;
;;;; TODO
;;;;
;;;; Replace s+ idiom with FileWalker indent print. (Maybe not?)
;;;; Make file-walker-files work without output file (for contains-llava-doc?).
;;;;

This *MUST* be outside of outer LET.

(-doc-
 (name llava-doc)
 (param source-dir)
 (param destination-dir)
 (description "..."))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;
;;;; State
;;;;

(let ((*fw* null)
      (*current-doc-package* 'nothing)
      (*current-builtin-package* 'nothing)
      (*llava-suffix* ".lva")
      (*doc-suffix* ".xml")
      (*tmp-contains-llava-doc?-file* "/tmp/junk-contains-llava-doc")
      (*found-llava-doc-true* "TRUE"))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;
;;;; First you parse then you format.
;;;;

(define _%-package-
  (lambda (fw doc)
    (file-walker-newline fw)
    (file-walker-print fw
     (format-package
      (set! *current-builtin-package* (parse-location doc))
      (parse-description doc)))))

(define _%-doc-
  (lambda (fw doc)
    (file-walker-newline fw)
    (file-walker-print fw
     (format-entry
      (parse-name doc)
      (parse-category doc)
      (parse-location doc)
      (parse-params doc)
      (parse-result doc)
      (parse-throws doc)
      (parse-description doc)))))

;;; The following only used for documenting builtin syntax and procedures.

(define _%-section-
  (lambda (fw doc)
    (file-walker-newline fw)
    (file-walker-print fw
     (format-section
      (parse-name doc)
      (parse-description doc)))))

(define _%-section-end-
  (lambda (fw)
    (file-walker-newline fw)
    (file-walker-print fw "</section>
")))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;
;;;; Walking directories, looking for files with -doc- documentation.
;;;;

(define llava-doc
  (lambda (source-root destination-root)
    (set! *fw*                      (new-FileWalker))
    (set! *current-builtin-package* 'nothing)
    (set! *current-doc-package*     'nothing)
    (for-each-dir
     (lambda (x) 
       (cond ((and (isFile x)
		   (endsWith (toString x) *llava-suffix*)
		   (contains-llava-doc? x))
	      (write-llava-doc x destination-root)
	      false)))
     source-root)))

(define write-llava-doc
  (lambda (source-file destination-root)
    (let* ((destination-dir (s+ destination-root "/" (getParent source-file)))
	   (filename-minus-suffix (substring (getName source-file)
					     0 (indexOf (getName source-file)
							*llava-suffix*)))
	   (destination-dir-and-filename 
	    (s+ destination-dir "/" filename-minus-suffix *doc-suffix*)))
      (cond ((exists-or-create destination-dir)
	     (extract-doc-files (toString source-file)
				destination-dir-and-filename)
	     (-println (s+ "Wrote: " destination-dir-and-filename)))
	    (else
	     (error (s+ "Cannot create: " destination-dir)))))))

(define exists-or-create
  (lambda (dir-name)
    (let ((dir (new 'java.io.File dir-name)))
      (cond ((exists dir))
	    (else (mkdirs dir))))))

;;;
;;; REVISIT - I wrote this with call/cc with the continuation passed
;;; to the dispatcher but got
;;; llava-> (-jbt)
;;; java.lang.Exception: ContinuationException
;;;	at org.llava.LlavaException.<init>(LlavaException.java:41)
;;;	at org.llava.impl.procedure.PrimCallCC$ContinuationException.<init>(PrimCallCC.java:83)
;;;	at org.llava.impl.procedure.PrimCallCC$Continuation.apply(PrimCallCC.java:69)
;;;
;;; The broken call/cc version is in RCS.
;;;

(define contains-llava-doc?
  (lambda (file)
    (let ((save-file-walker-dispatcher   (get-dispatcher *fw*)))
      (try
       (begin
	 (set-dispatcher! *fw* contains-llava-doc?-dispatch)
	 (file-walker-files *fw* 
			    (toString file) *tmp-contains-llava-doc?-file*)
	 false)
       (catch (java.lang.Exception e)
	 (if (eq? (getMessage e) *found-llava-doc-true*)
	     true
	     false))
       (finally
	(set-dispatcher! *fw* save-file-walker-dispatcher))))))
       
(define contains-llava-doc?-dispatch
  (lambda (fw sexpr indent)
    (if (and (pair? sexpr)
	     (eq? (car sexpr) '-doc-))
	(throw (new 'java.lang.Exception *found-llava-doc-true*))
	null)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;
;;;; Extracting and writing results to a file.
;;;;

(define extract-doc-files 
  (lambda (inFileName outFileName)
    (let ((save-file-walker-dispatcher   (get-dispatcher   *fw*))
	  (save-file-walker-print-header (get-print-header *fw*))
	  (save-file-walker-print-footer (get-print-footer *fw*)))
      (try
       (begin
	 (set-dispatcher!   *fw* extract-doc-dispatch)
	 (set-print-header! *fw* extract-doc-print-header)
	 (set-print-footer! *fw* extract-doc-print-footer)
	 (file-walker-files *fw* inFileName outFileName))
       (finally
	 (set-dispatcher!   *fw* save-file-walker-dispatcher)
	 (set-print-header! *fw* save-file-walker-print-header)
	 (set-print-footer! *fw* save-file-walker-print-footer))))))

;; convenience for testing
(define extract-doc
  (lambda (fw forms)
    (set-file-walker-dispatcher!   fw extract-doc-dispatch)
    (set-file-walker-print-header! fw extract-doc-print-header)
    (set-file-walker-print-footer! fw extract-doc-print-footer)
    (file-walker-with-indent fw forms 0)))

(define extract-doc-dispatch
  (lambda (fw sexpr indent)
    (cond ((pair? sexpr)
	   (case (car sexpr)
	     ((package)       (set! *current-doc-package* (cadr sexpr)))
	     ((-package-)     (_%-package-     fw (cdr sexpr)))
	     ((-section-)     (_%-section-     fw (cdr sexpr)))
	     ((-section-end-) (_%-section-end- fw))
	     ((-doc-)         (_%-doc-         fw (cdr sexpr)))
	     (else            null))))))

(define extract-doc-print-header
  (lambda (fw out inFileName outFileName)
    (println out "<!--")
    (print out " Extracted on ")
    (println out (toString (new 'java.util.Date)))
    (print out " Extracted from ")
    (println out inFileName)
    (print out " Extracted to   ")
    (println out outFileName)
    (println out "-->")))

(define extract-doc-print-footer
  (lambda (fw out inFileName outFileName)
    (println out (format-package-end)) ;; REVISIT - kludge
    (println out)
    (println out "<!-- End of file. -->")))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 
;;;;
;;;; format to DocBook
;;;;

(define format-package
  (lambda (location description)
    (-dprint (list 'format-package location description))
    (s+
     (format-package-begin location)
     (format-description description))))

(define format-section
  (lambda (name description)
    (-dprint (list 'format-section name description))
    (s+
     (format-section-begin name)
     (format-description description))))

(define format-entry
  (lambda (name category location params result throws description)
    (-dprint (list 'format-entry name category location params result throws description))
    (s+
     (format-refentry-begin location name)
     (format-index category name)
     (format-refentry1-begin)
     (format-template name params)
     (format-category-and-location category location)
     (format-description description)
     (format-refentry1-end)
     (format-refentry-end))))

;;;
;;; The "package" attribute is non-standard.
;;; It is used by make-package-index to distinquish package sections
;;; from other uses of section.
;;;

(define format-package-begin
  (lambda (location)
    (set! location (strip-import location))
    (s+
"<!-- ===================================================================== -->
<!-- ===================================================================== -->
<section id=\"" location "\"
         package=\"" location "\">
  <title>" location "</title>
")))

(define format-section-begin
  (lambda (name)
    (set! name (strip-import name))
    (s+
"<!-- ===================================================================== -->
<!-- ===================================================================== -->
<section id=\"" name "\">
  <title>" name "</title>
")))

(define format-package-end
  (lambda ()
    "</section>"))

;;;
;;; The "package" and "procedure" attributes are non-standard.
;;; They are used by make-procedure-index.
;;;

(define format-refentry-begin
  (lambda (location name)
    (s+
"<!-- ================================================== -->
<refentry id=\"" (s+ (strip-import location) "." (toString name)) "\"
          package=\"" (strip-import location) "\"
          procedure=\"" (toString name) "\">
")))

(define (format-index category name)
  (s+
   "  <indexterm><primary>" (toString category) "</primary>
       <secondary>" (toString name) "</secondary></indexterm>
"))

(define format-refentry1-begin
  (lambda ()
    "  <refsect1>
"))

(define (format-template name params)
  (s+
"    <para><phrase id=\"template\">
      (<function>" (toString name) "</function>"
   (let ((result ""))
     (for-each (lambda (x)
		 (set! result (s+ result 
				  " <parameter>" 
				  (toString (second x))
				  "</parameter>")))
	       params)
     result)
")
    </phrase></para>
"))

(define format-category-and-location
  (lambda (category location)
    (s+
"    <para>
      <phrase id=\"category\">
        -- " (toString category) "
      </phrase>
      <phrase id=\"location\">
        -- <code>" (toString location) "</code>
      </phrase>
    </para>
")))

(define format-description
  (lambda (description)
    (let ((result ""))
      (for-each (lambda (x)
		  (set! result (s+ result (format-text-or-example x))))
		description)
      result)))

(define format-text-or-example
  (lambda (x)
    (cond ((and (pair? x) (eq? (car x) 'example))
	   (format-example (cdr x)))
	  ((pair? x)
	   (error "expected example"))
	  (else ;; REVISIT - check for string? - but make predicates builtin
	   (s+ "    <para>" x "</para>
")))))

(define format-example
  (lambda (example)
    (let ((result "
    <screen>
"))
      (for-each (lambda (x)
		  (set! result (s+ result x "
")))
		example)
      (s+ result "</screen>
"))))

(define format-refentry1-end
  (lambda () "  </refsect1>
"))

(define format-refentry-end
  (lambda () "</refentry>
"))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;
;;;; Parsing -doc-
;;;;

(define parse-name
  (lambda (doc) (second (assq 'name doc))))

(define parse-category
  (lambda (doc)
    (let ((category (assq 'category doc)))
      (-if category
	   (second category)
	   'procedure))))

(define parse-location
  (lambda (doc) 
    (-dprint (list 'parse-location 
		   'doc doc 
		   '*current-builtin-package* *current-builtin-package*
		   '*current-doc-package*     *current-doc-package*))
    (let ((location (assq 'location doc)))
      (cond (location 
	     (second location))
	    ((not (eq? *current-doc-package* 'nothing))
	     (list 'import *current-doc-package*))
	    (else
	     *current-builtin-package*)))))

(define parse-params
  (lambda (doc) (collect 'param doc)))

(define parse-result
  (lambda (doc) (collect 'result doc)))

(define parse-throws
  (lambda (doc) (collect 'throws doc)))

(define parse-description
  (lambda (doc) 
    (let ((description (assq 'description doc)))
      (-if description
	   (cdr description)
	   '()))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;
;;;; Implementation utilities
;;;;

(define (collect x doc)
  (let ((result (list 'dummy))) ;; NB: make a fresh list
    (do ((d doc (cdr d))
	 (rptr result 
	       (if (collect-match (caar d) x)
		   (begin
		     (setCdr rptr (cons (car d) '()))
		     (cdr rptr))
		   rptr)))
	((null? d) (cdr result)))))

(define collect-match
  (lambda (d x)
    (if (pair? x)
	(-if (memq d x)
	     true
	     false)
	(eq? x d))))

(define strip-import
  (lambda (location)
    (replaceAll (replaceAll (toString location) "\\(import " "" )
		"\\)" "" )))

(define -dprint
  (lambda (msg)
    ;(-println msg)
    null
    ))

) ;; End of LET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;
;;;; Test
;;;;

(-comment-
;; cd .system
(import org.llava.lib.LlavaDoc)
(llava-doc "./org" "./doc/.generated")
(extract-doc-files (s+ (llava-dir) "/org/llava/impl/llava.lva") "/tmp/junk.xml")
(extract-doc-files (s+ (llava-dir) "/org/llava/lib/scm/Lists.lva") "/tmp/junk.xml")
(extract-doc-files (s+ (llava-dir) "/org/llava/lib/LlavaDoc.lva") "/tmp/junk.xml")
(extract-doc
 '((package foo)
   (-doc-
    (name fooo)
    (param obj))
   (-doc-
    (name kill-current-thread)
    (description "Calls deprecated <function>stop</function> on the
      current thread."))))

(extract-doc
 '((package org.foo)
   (-doc-
    (name apply-fail-on-false)
    (param procedure "...")
    (param args "list")
    (result obj "like (<function>apply</function> <parameter>procedure</parameter> <parameter>args</parameter>)")
    (throws LlavaException "If result is false.")
    (description 
     "Identical to <function>apply</function> except it raises an
      exception if the result is false."
     (example "(apply (lambda (x) (pair? x)) '((1 . 2))) &lva-et; true")
     (example "(apply (lambda (x) (pair? x)) '(1)) &lva-te;")
    ))))

(extract-doc
 '((-doc-
    (name equal?)
    (param obj1)
    (param obj2)
    (location org.llava))))

)

;;; End of file.
